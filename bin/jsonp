#! /usr/bin/env perl

use 5.014;
use autodie qw< :all >;
use warnings FATAL => 'all';

use JSON;
use Method::Signatures;

$| = 1;


my $prog = shift or die("usage: $0 'perl program to process json lines' [file ...]");
our $j;
our %count_results;

my $processor = q{
	sub process
	{
		local $_ = {};
		no warnings 'exiting';
		##prog##;
		say to_json($_) if %$_;
	}
	1;
};
$processor =~ s/##prog##/$prog/;
eval $processor or die("$0: compile error: $@");

while ( <> )
{
	s/^.*?{/{/;
	$j = from_json($_);
	process();
}

if (%count_results)
{
	say "$_ : $count_results{$_}" foreach sort keys %count_results;
}


func copy (@keys)
{
	my $dest = $_;
	local $_;
	$dest->{$_} = $j->{$_} foreach @keys;
}

func clone ()
{
	my $dest = $_;
	local $_;
	$dest->{$_} = $j->{$_} foreach keys %$j;
}

func count ($value)
{
	++$count_results{$value};
}
