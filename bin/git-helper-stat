#! /usr/bin/env perl

use myperl::Pxb;
use autodie ':all';


opts <<'-';
	-S : don't show stash list
	used by `vc stat-plus`; do not use independently
-


my $cur_branch = sh(git => 'rev-parse',    '--abbrev-ref',     'HEAD');
my $mainline   = sh(git => 'symbolic-ref', 'refs/remotes/origin/HEAD') =~ s{^.*/}{}r;

my $stat    = sh(git => status =>         );
my $stash   = sh(git => stash  => list => );
my $commits = sh('git-helper-log -LNs'    );
   $commits = "Unpushed commits:\n$commits" if $commits;
if ( not $commits and $cur_branch ne $mainline )
{
	$commits = sh('git-helper-log -bNs');
	$commits =~ s/\A\n*/Branch commits:\n/;
}

say join("\n\n", map { filter_for_length($_) } $stat, $OPT{S} ? () : $stash, $commits);


sub numlines { scalar shift =~ tr/\n// }

sub filter_for_length
{
	my ($output) = @_;
	return $output if numlines($output) < 35;

	my @new;
	foreach ( split("\n\n", $output) )
	{
		state $color = qr/(?: \e\[ [\d;]*  m )/x;
		state $line  = qr/(?:          .* \n )/x;

		s|^($line{3}\s*$color?)($line{20,}.*?)($color?)$|$1 . "...{ @{[ numlines($2) ]} more lines }..." . ($3 // '')|e;
		push @new, $_;
	}
	return join("\n\n", @new);
}
