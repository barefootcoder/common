#! /usr/bin/env perl

use myperl::Pxb;
use autodie ':all';
use charnames ':full';


const our $DEFAULT_FILE_LIMIT => 5;

opts <<"-";
	[-l <limit>]
	-l : file limit (<limit> files or more will *not* be reduced to parent dir; default: $DEFAULT_FILE_LIMIT)
	reads list of files from STDIN (e.g. find . -type f | $ME)
	consolidates the counts into parent dirs until one of two things is true:
	* there are no additional files to consolidate with them, or
	* a directory indicates at least <limit> files
-

my $file_limit = $OPT{'l'} // $DEFAULT_FILE_LIMIT;


my @files = <>;
chomp @files;

my $dir_files = {};
$dir_files->{ $_->dirname }++ foreach map { file($_) } @files;
while (1)
{
	debuggit(6 => "current list:", DUMP => $dir_files);
	my $count = keys %$dir_files;
	$dir_files = reduce_list($dir_files, $file_limit);
	last if keys %$dir_files == $count;
}

say sprintf "%5d : $_", $dir_files->{$_} foreach sort keys %$dir_files;


sub reduce_list
{
	my ($dir_files, $limit) = @_;

	my %parent_files =
			map		{ $_ => ($dir_files->{$_} // 0)		}				# initial count for parent
			map		{ path($_)->dirname					}				# get parent
					keys %$dir_files;
	$parent_files{ path($_)->dirname } += $dir_files->{$_} foreach keys %$dir_files;
	foreach my $file (sort { length $b <=> length $a } map { path($_) } keys %$dir_files)
	{
		my $parent = $file->dirname;
		next if $parent =~ m|^[^/]*/?$|;			# don't consolidate up to the top-level dir

		debuggit(5 => "before [limit $limit] --> file:", $file,
				"=>", $dir_files->{$file}, "/ parent:", $parent,
				"=>", $parent_files{$parent});
		if ($parent_files{$parent} > $dir_files->{$file} and $dir_files->{$file} < $limit)
		{
			$dir_files->{$parent} += delete $dir_files->{$file};
		}
		debuggit(5 => "\N{BOX DRAWINGS HEAVY UP AND RIGHT}after [limit $limit] --> file:", $file,
				"=>", $dir_files->{$file}, "/ parent:", $parent,
				"=>", $dir_files->{$parent});
	}

	return $dir_files;
}
