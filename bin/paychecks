#! /usr/bin/env perl

use myperl DEBUG => 1;

use Getopt::Std;
use Tie::IxHash;


my $opts = {};
getopts('vh', $opts);

if ($opts->{h})
{
	say STDERR "usage: ", file($0)->basename, " -h | [-v] startdate [enddate]";
	say STDERR "       -v : print each paycheck separately (may cause voluminous output)";
	say STDERR "     default end date is 3 days from today";
	say STDERR "     date format is anything understood by Date::Parse";
	exit;
}


tie my %FAMILY, 'Tie::IxHash' =>
(
	Buddy	=>	'11/5/1966',
	Christy	=>	'8/30/1971',
	Random	=>	'9/8/1998',
	Perrin	=>	'3/8/2006',
	Merrick	=>	'3/31/2012',
);
$_ = str2time($_) foreach values %FAMILY;

const my $HOUR => 60 * 60;
const my $DAY => 24 * $HOUR;
const my $WEEK => 7 * $DAY;
const my $YEAR => 365.25 * $DAY;


my $last_date = str2time(shift) or die("must supply last paycheck date");
# target can be specified as a hard cutoff, or defaults to a point about 3 days from now
my $target = str2time(shift) // str2time(time2str('%m/%d/%Y', time)) + 3 * $DAY;
foreach (keys %FAMILY)
{
	my $dow = time2str('%A', $FAMILY{$_});
	my $data = { weeks_owed => 0, weekly_total => 0, num_bdays => 0, bday_total => 0, };
	$data->{age} = round(UP => ($last_date - $FAMILY{$_}) / $YEAR);
	say "$_ : paid on $dow, \$$data->{age} per week (at start of period)";

	# go backwards till we find the previous payday
	my $prev_payday;
	for ($prev_payday = $last_date; time2str('%A', $prev_payday) ne $dow; $prev_payday -= $DAY)
	{
	}

	# now go forwards from previous payday until we hit a point later than the target,
	# and pay attention to if we pass any birthdays
	# bump it by 2 hours to deal with DST
	# and then bump the target to deal with the adjustment above
	my ($final_payday, $last_payday);
	for ($last_payday = $prev_payday + 2 * $HOUR + $WEEK; $last_payday < $target + $DAY; $last_payday += $WEEK)
	{
		calc_paycheck($last_payday, $data);
		$final_payday = $last_payday;
	}

	say "    last paycheck : ", time2str('%m/%d/%Y', $prev_payday);
	say "   final paycheck : ", time2str('%m/%d/%Y', $final_payday);
	say "    next paycheck : ", time2str('%m/%d/%Y', $last_payday);
	say "    weekly owed   : $data->{weeks_owed} weeks totalling \$$data->{weekly_total}";
	say "    bday bonus    : $data->{num_bdays} bdays totalling \$$data->{bday_total}";
}


func calc_paycheck ($payday, $data)
{
	state $fmt = "*** \$%7.2f paid on %s ***\n";
	my $pay_date = time2str('%m/%d/%y', $payday);
	debuggit(4 => "payday:", $pay_date);

	my $birthday_date = join('/', time2str('%m/%d', $FAMILY{$_}), time2str('%Y', $payday));
	my $birthday = str2time($birthday_date);
	if ($payday > $birthday and $payday - $birthday < $WEEK)			# if birthday is less than a week prior ...
	{
		# ... then we passed a birthday: up the weekly amount and add a bonus
		my $amount = $data->{age} * 25;
		printf $fmt, $amount, $birthday_date if $opts->{v};
		++$data->{num_bdays};
		$data->{bday_total} += $amount;
		++$data->{age};
		debuggit(3 => "think last bday", time2str('%m/%d/%y', $birthday), "was this week:",
				"difference", ($payday - $birthday) / $DAY);

		if (time2str('%m/%d', $payday) eq time2str('%m/%d', $FAMILY{$_}))
		{
			# birthday falls on a payday; this week doesn't count
			# so don't add to the total this week
			next;
		}
	}

	printf $fmt, $data->{age}, $pay_date if $opts->{v};
	++$data->{weeks_owed};
	$data->{weekly_total} += $data->{age};
}
