#! /usr/bin/env perl

BEGIN { eval { require 5.14.0 } or exec "vim", @ARGV }
use 5.14.0;
use warnings FATAL => 'all';

my $timer_line_sep = "=" x 40;
my $svn_line_sep = qr{ -- .* ignored-- $ }x;
my $cvs_line_sep = "CVS: " . "-" x 70;
my $git_line_sep = quotemeta('# Please enter the commit message for your changes.');

my @lines;
if ($ARGV[0])
{
	if (open(FILE, $ARGV[0]))
	{
		push @lines, scalar(<FILE>) // () for 1..5;
		close(FILE);
	}
}

if (not @lines)
{
	exec "vim", @ARGV;
}
elsif (grep { /<html>/i } @lines)
{
	say STDERR "using web startup (checked ", scalar(@lines), " lines)";
	exec qw<vim -u /home/buddy/.viwebrc>, @ARGV;
}
elsif (grep { /<\w+ METAx>/i } @lines)
{
	exec qw<vim -s /home/buddy/.vim-genhtml>, @ARGV;
}
elsif (grep { /(PRE-QA|TESTING) NOTES/i } @lines)
{
	exec 'vim', '+', '-c', 'set comments=', @ARGV;
}
elsif ($lines[0] eq "\n" and $lines[1] =~ /^($timer_line_sep|$svn_line_sep|$cvs_line_sep|$git_line_sep)/)
{
	exec 'vim', '+1', '-c', 'startinsert', @ARGV;
}
else
{
	exec "vim", @ARGV;
}
