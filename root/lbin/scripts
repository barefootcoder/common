#! /usr/bin/perl

use strict;
use warnings;
require 5.8.0;

use File::Find;
use Getopt::Std;
use File::Basename;


my $ME = basename($0);
sub for_every_file(&@);


my $mode = 'long';
my $pattern;
my $opt = {};
my $opt_ok = getopts("bg:tfivpsnrlDh", $opt);
$mode = 'bare' if $opt->{b};
$mode = 'list' if $opt->{l};
$mode = 'grep', $pattern = $opt->{g} if $opt->{g};
my $perlonly = $opt->{p};
my $shellonly = $opt->{s};
my $nonex = $opt->{n};
my $recursive = $opt->{r};
my $verbose = $opt->{v};
my $lstime = $opt->{t} ? '-tr' : '-1';
my $greptype = $opt->{f} ? '-F' : '-P';
$greptype .= 'i' if $opt->{i};
$greptype .= ($verbose ? 'H' : 'l');
my $DEBUG = $opt->{D};
if ($opt->{h} or not $opt_ok)
{
	print STDERR "usage: $ME [-vnpsrD] [-t | -b | -l | -[fi]g 'pattern'] [dir]\n";
	print STDERR "         -v:  show progress and matching lines of search (with -g)\n";
	print STDERR "         -n:  include non-executable files as well\n";
	print STDERR "         -p:  include only Perl scripts\n";
	print STDERR "         -s:  include only shell (sh/bash/ksh) scripts\n";
	print STDERR "         -r:  recurse into subdirs\n";
	print STDERR "         -l:  list (show only subdirs which contain scripts; most useful with -r)\n";
	print STDERR "         -t:  sort by time (oldest first; doesn't work for >1,000 scripts)\n";
	print STDERR "         -b:  bare (show only script names)\n";
	print STDERR "         -g:  show only scripts containing pattern (as `grep -P`)\n";
	print STDERR "         -f:  fast search with -g (as `fgrep` instead of `grep -P`)\n";
	print STDERR "         -i:  case insensitive search with -g\n";
	print STDERR "         -D:  debug (show all external command run)\n";
	print STDERR "         searches . if dir not specified\n";
	exit;
}

push @ARGV, "." if not @ARGV;

my @scripts;
foreach my $filespec (@ARGV)
{
	if ($recursive)
	{
		find( { wanted => \&check_dir, no_chdir => 1 }, $filespec);
	}
	else
	{
		check_dir($filespec);
	}
}

print STDERR "$ME: no scripts found\n" and exit 1 unless @scripts;

$; = " ";
if ($mode eq 'bare' or $mode eq 'list')
{
	print "$_\n" foreach @scripts;
}
elsif ($mode eq 'long')
{
	for_every_file
	{
		run_command('ls', '-lF', $lstime, @_) == -1 and die("can't execute ls");
	}
	@scripts;
}
elsif ($mode eq 'grep')
{
	for_every_file
	{
		run_command('grep', $greptype, '-e', $pattern, @_) == -1 and die("can't execute grep");
	}
	@scripts;
}


sub check_dir
{
	my $filespec = @_ ? shift : $File::Find::name;
	if (-d $filespec)
	{
		# skip problematic dirs
		if ($filespec eq '/proc' or $filespec eq '/dev' or $filespec =~ m'/\.git/')
		{
			print STDERR "skipping $filespec\n" if $verbose;
			$File::Find::prune = 1;
			return;
		}

		my @files = glob("$filespec/*");
		@files = grep { -x and not -d } @files unless $nonex;
		return unless @files;

		print STDERR "now checking $filespec/*\n" if $verbose;
		for_every_file
		{
			my @cmd_args = (qw<file -N -->, @_);
			print STDERR ":: @cmd_args ::\n" if $DEBUG;
			open(FILE, "-|", @cmd_args) or die("can't open pipe to file command");
			while ( <FILE> )
			{
				if ( /:.*commands text/ or /:.*script/ )
				{
					next if $perlonly and not /:.*\bperl\b/i;
					next if $shellonly and not /:.*\b(?:ba|k)?sh\b/i and not /:.*\bshell\b/i;

					if ($mode eq 'list')
					{
						push @scripts, $filespec;
						return;
					}
					else
					{
						my ($script) = /^(.+?):/;
						push @scripts, $script;
					}
				}
			}
			close(FILE);
		}
		@files;
	}
}


sub run_command
{
	my @cmd_args = @_;
	print STDERR ":: @cmd_args ::\n" if $DEBUG;
	system(@cmd_args);
}


sub for_every_file (&@)
{
	my $sub = shift;
	while (@_ > 1000)
	{
		$sub->(@_);
		splice @_, 0, 1000;
	}
	$sub->(@_) if @_;
}
