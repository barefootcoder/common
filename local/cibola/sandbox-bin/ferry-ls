#! /usr/bin/env perl

use myperl::Script;
use autodie ':all';

# CE lib modules may be insufficient for Date::Easy
BEGIN { @INC = grep { not m|/CE/| } @INC }

use Path::Tiny;
use Date::Easy;
use PerlX::bash;

const my $CE     => 'campus-explorer';
const my $S3_DIR => 's3://backup.campusexplorer.com/sandbox/bburden';
const my $PREFIX => 'CE-branch';


opts <<'-';
	{ -c | [-al | -t] [-R <repo>] [{ -e <pattern> | <pattern> }] }
	-c : print basenames only (good for tab-completion); implies pattern of `.'
	-a : show all versions of a file (default: show only the latest)
	-l : show long name (full S3 URL) (default: show base name and date)
	-t : sort by time (oldest to newest; default: sort alphabetically by base)
	-R : show for this <repo> (default: repo of current directory)
	-e : next arg is the <pattern>, even if it starts with -
	<pattern> : Perl-style regex (default: `.')
-
usage_error("can't use `-t` with `-l`") if $OPT{t} and $OPT{l};

my $regex = $OPT{c} ? '.' : $OPT{e} // shift // '.';
$regex = qr/$regex/;


# current repo check
(bash \string => git => config => '--get' => 'remote.origin.url') =~ m|^.*?:(.*?)/(.*)\.git$|
		or fatal("this does not appear to be a valid repo directory");
my ($owner, $repo) = ($1,$2);
$owner eq $CE or fatal("this repo is not owned by $CE");
$OPT{R} //= $repo;

my $prefix = $OPT{R} ? "$PREFIX\@$repo:" : "$PREFIX-";

my %files;
foreach (bash \lines => "aws s3 ls $S3_DIR/$prefix")
{
	my $filename = (split(' '))[-1];
	my ($base, $date) = $filename =~ /^$prefix(.*)-(\d+)/;
	fatal("can't figure out date for file! [$_]") unless $date;
	push @{ $files{$base} }, $OPT{l} ? "$S3_DIR/$filename" : date($date);
}

my @output;
foreach my $base (sort grep { /$regex/ } keys %files)
{
	if ($OPT{c})
	{
		say $base;
	}
	elsif ($OPT{l})
	{
		my @urls = sort @{ $files{$base} };
		@urls = ($urls[-1]) unless $OPT{a};
		push @output, @urls;
	}
	else
	{
		my @dates = sort map { $_->as('/Ymd') } @{ $files{$base} };
		@dates = ($dates[-1]) unless $OPT{a};
		push @output, map { "  [$_]  $base" } @dates;
	}
}

@output = sort @output if $OPT{t};
say foreach @output;
