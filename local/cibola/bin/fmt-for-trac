#! /usr/bin/env perl

use 5.14.0;
use autodie ':all';
use warnings FATAL => 'all';

use Text::Tabs; $tabstop = 4;


my $pre_re = qr/^\.$/;

my $in_pre = 0;					# 0 == not in pre, 1 == indented pre block, 2 == explicit pre block
my $in_list = 0;
while (<>)
{
	$_ = expand($_);

	if (( /$pre_re/ or /^\s+(.)/ and !($1 ~~ ['*','#']) ) and not $in_pre and not $in_list)
	{
		if ( /$pre_re/ )
		{
			$_ = "{{{\n";
			$in_pre = 2;
		}
		else
		{
			say '{{{';
			s/^    //;
			$in_pre = 1;
		}
	}
	elsif ($in_pre == 1 and /^$/ or $in_pre == 2 and /$pre_re/ )
	{
		$in_pre == 1 ? say '}}}' : ($_ = "}}}\n");
		$in_pre = 0;
	}
	elsif ($in_list and /^$/)
	{
		$in_list = 0;
	}
	elsif ($in_pre == 1)
	{
		s/^    //;
	}
	elsif (not $in_pre)
	{
		s/^  (\s*\*\s+)/$1/ and $in_list = 1;							# bullet lists
		s/^  (\s*)#(\s+)/${1}1.$2/ and $in_list = 1;					# numbered lists
		s/^  // if $in_list and not /^\s+[*#]/;							# continuation of bullet items

		s/\*\b(.+?)\*(?=\W)/'''$1'''/g;									# bold
		s/(^|\W)_(.+?)_(\W|$)/$1''$2''$3/g;								# italics
		s/\[(.*?)\|(.*?)\]/[$1 $2]/g;									# links
		s/(#\d)(?!\d)/!$1/g;											# numbered things that aren't tickets
	}

	print;
}
